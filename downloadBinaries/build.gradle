
buildscript {
    repositories {
        if(project.properties.BUILDREPO) {
            maven {
                url "https://plugins.gradle.org/m2/"
            }
            mavenLocal()
            jcenter()
        } else {
            ivy {
                url file('../repo')
                layout 'gradle'
            }
        }
    }
    dependencies {
        classpath "org.ysb33r.gradle:ivypot:0.4"
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta6'
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

import org.ysb33r.gradle.vfs.tasks.VfsCopy

// This can only be used whilst online

apply plugin : 'org.ysb33r.ivypot'
apply plugin : 'org.ysb33r.vfs'

task getGradle( type : VfsCopy ) {
    description 'Download Gradle'
    into file('repo/gradle')
    from "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
    outputs.upToDateWhen {
        file("repo/gradle/gradle-${gradleVersion}-all.zip").exists()
    }
    onlyIf { !gradle.startParameter.isOffline() && project.properties.BUILDREPO }
}

task getGroovy( type : VfsCopy ) {
    description 'Download Groovy'
    into file('repo/groovy')

    from "http://dl.bintray.com/groovy/maven/apache-groovy-docs-${groovyVersion}.zip"
//    from "http://dl.bintray.com/groovy/maven/apache-groovy-sdk-${groovyVersion}.zip"
    from "http://dl.bintray.com/groovy/maven/apache-groovy-binary-${groovyVersion}.zip"

    outputs.upToDateWhen {
        file("repo/groovy/apache-groovy-docs-${groovyVersion}.zip").exists() &&
            file("repo/groovy/apache-groovy-binary-${groovyVersion}.zip").exists()
    }

    onlyIf { !gradle.startParameter.isOffline() && project.properties.BUILDREPO }
}

configurations {
    examples
}

dependencies {
    examples 'junit:junit:4.1'
    examples 'org.codehaus.groovy:groovy-all:2.4.3'
    examples 'org.spockframework:spock-core:1.0-groovy-2.4'
    examples "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1", {
        exclude module : 'groovy-all'
    }
    examples 'com.athaydes:spock-reports:1.2.10', {
        transitive = false
    }
    examples 'org.slf4j:slf4j-api:1.7.13'
    examples 'org.slf4j:slf4j-simple:1.7.13'
    examples 'io.ratpack:ratpack-test:1.2.0'
    examples 'software.betamax:betamax-junit:2.0.0-alpha-1'
    examples 'org.ysb33r.testables:theinternet-webtest:0.49.1-r1-SNAPSHOT'

    examples "org.gebish:geb-spock:0.13.0"
    examples "org.seleniumhq.selenium:selenium-api:2.53.0"
    examples "org.seleniumhq.selenium:selenium-support:2.53.0"
    examples "org.seleniumhq.selenium:selenium-firefox-driver:2.53.0"
}

syncRemoteRepositories {
    repoRoot file('repo')

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenLocal()
    }

    configurations 'examples'

    onlyIf { !gradle.startParameter.isOffline() && project.properties.BUILDREPO }

    includeBuildScriptDependencies = true
}

task download {
    dependsOn getGradle, getGroovy, syncRemoteRepositories
}


