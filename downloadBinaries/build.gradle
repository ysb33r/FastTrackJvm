
buildscript {
    repositories {
        if(System.properties.BUILDREPO) {
            maven {
                url "https://plugins.gradle.org/m2/"
            }
            mavenLocal()
            jcenter()
        } else {
            ivy {
                url file('../repo')
                layout 'gradle'
            }
        }
    }
    dependencies {
        classpath "org.ysb33r.gradle:ivypot:0.4"
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta8'
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

import org.ysb33r.gradle.vfs.tasks.VfsCopy

// This can only be used whilst online

ext {
    buildingRepo = { !gradle.startParameter.isOffline() && System.properties.BUILDREPO }
    repoDir = '../repo'
}

apply plugin : 'org.ysb33r.ivypot'
apply plugin : 'org.ysb33r.vfs'

task getGradle( type : VfsCopy ) {
    description 'Download Gradle'
    into file("${repoDir}/gradle")
    from "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
    outputs.upToDateWhen {
        file("${repoDir}/gradle/gradle-${gradleVersion}-all.zip").exists()
    }
    onlyIf buildingRepo
}

task getGroovy( type : VfsCopy ) {
    description 'Download Groovy'
    into file("${repoDir}/groovy")

    from "http://dl.bintray.com/groovy/maven/apache-groovy-docs-${groovyVersion}.zip"
//    from "http://dl.bintray.com/groovy/maven/apache-groovy-sdk-${groovyVersion}.zip"
    from "http://dl.bintray.com/groovy/maven/apache-groovy-binary-${groovyVersion}.zip"

    outputs.upToDateWhen {
        file("${repoDir}/groovy/apache-groovy-docs-${groovyVersion}.zip").exists() &&
            file("${repoDir}/groovy/apache-groovy-binary-${groovyVersion}.zip").exists()
    }

    onlyIf buildingRepo
}

configurations {
    examples
    jruby17
    jcommander
    groovy18
}

dependencies {
    examples 'junit:junit:4.1'
    examples 'org.codehaus.groovy:groovy-all:2.4.3'
    examples 'org.spockframework:spock-core:1.0-groovy-2.4'
    examples "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1", {
        exclude module : 'groovy-all'
    }
    examples 'com.athaydes:spock-reports:1.2.10', {
        transitive = false
    }
    examples 'org.slf4j:slf4j-api:1.7.13'
    examples 'org.slf4j:slf4j-simple:1.7.13'
    examples 'io.ratpack:ratpack-test:1.2.0'
    examples 'software.betamax:betamax-junit:2.0.0-alpha-1'
    examples 'org.ysb33r.testables:theinternet-webtest:0.49.1-r1-SNAPSHOT'

    examples "org.gebish:geb-spock:0.13.0"
    examples "org.seleniumhq.selenium:selenium-api:2.53.0"
    examples "org.seleniumhq.selenium:selenium-support:2.53.0"
    examples "org.seleniumhq.selenium:selenium-firefox-driver:2.53.0"

    examples 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    examples 'org.asciidoctor:asciidoctorj:1.5.3.2'
    examples 'org.asciidoctor:asciidoctorj-groovy-dsl:1.0.0.preview2', {
        exclude module : 'groovy-all'
    }

    jcommander 'com.beust:jcommander:1.35'
    groovy18 'org.codehaus.groovy:groovy-all:1.8.9'
    jruby17 'org.jruby:jruby-complete:1.7.21'
}

syncRemoteRepositories {
    repoRoot file("${repoDir}")

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenLocal()
    }

    configurations 'examples', 'jruby17', 'jcommander','groovy18'

    onlyIf buildingRepo

    includeBuildScriptDependencies = true
}

task download {
    dependsOn getGradle, getGroovy, syncRemoteRepositories
}


