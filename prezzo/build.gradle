
buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
        classpath "org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta6"
        classpath "com.github.jruby-gradle:jruby-gradle-plugin:1.1.4"
        classpath "commons-httpclient:commons-httpclient:3.1"

    }
}



apply plugin : 'groovy'
apply plugin : 'org.asciidoctor.convert'
apply plugin : 'org.ysb33r.vfs'
apply plugin : 'com.github.jruby-gradle.base'


ext {
    downloadDir = new File(buildDir, 'download')

    revealjsVersion = 'master'
    revealjsTemplateVersion = 'master'
    revealjsDir = new File(downloadDir, 'reveal.js')
    revealjsTemplateDir = new File(downloadDir, 'revealjs-backends/revealjs')

    examplesDir = file("${buildDir}/examples")

    groovyVer = '2.4.3'
    spockVer = '1.0-groovy-2.4'
}

ext {
    tapeDir = file('src/betamaxTest/resources/betamax/tapes')
}

sourceSets {
    betamaxTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/betamaxTest/groovy')
        }
        resources.srcDir file('src/betamaxTest/resources')
    }
}

configurations {
    betamaxTestCompile.extendsFrom testCompile
    betamaxTestRuntime.extendsFrom testRuntime
}

task betamaxTest(type: Test) {
    testClassesDir = sourceSets.betamaxTest.output.classesDir
    classpath = sourceSets.betamaxTest.runtimeClasspath
}

check.dependsOn betamaxTest

repositories {
    mavenLocal()
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVer}"
    compile 'org.nanohttpd:nanohttpd:2.3.0'
    compile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1", {
        exclude module : 'groovy-all'
    }
    testCompile "org.spockframework:spock-core:${spockVer}", {
        exclude module : 'groovy-all'
    }
    betamaxTestCompile "org.spockframework:spock-core:${spockVer}", {
        exclude module : 'groovy-all'
    }
    // tag::spockreports[]
    testCompile 'com.athaydes:spock-reports:1.2.10', {
        transitive = false
    }
    testCompile 'org.slf4j:slf4j-api:1.7.13'
    testCompile 'org.slf4j:slf4j-simple:1.7.13'
    // tag::spockreports[]
    // tag::ratpacktest[]
    testCompile 'io.ratpack:ratpack-test:1.2.0'
    // end::ratpacktest[]
    // tag::betamax[]
    betamaxTestCompile 'software.betamax:betamax-junit:2.0.0-alpha-1'
    // end::betamax[]

    testCompile 'org.ysb33r.testables:theinternet-webtest:0.49.1-r1-SNAPSHOT'
    // tag::geb[]
    testCompile "org.gebish:geb-spock:0.13.0"
    testCompile "com.machinepublishers:jbrowserdriver:0.10.4"
    testRuntime "org.seleniumhq.selenium:selenium-support:2.53.0"
    testRuntime "org.seleniumhq.selenium:selenium-firefox-driver:2.53.0"
    // end::geb[]
}

test {
    systemProperties "geb.build.reportsDir": "$reportsDir/geb"
}

betamaxTest {
    systemProperties 'BETAMAX_TAPEDIR' : tapeDir.absolutePath
    systemProperties 'BETAMAX_PROXYPORT' : 55555

    systemProperties 'BINTRAY_USERNAME' : project.properties.bintrayUserName
    systemProperties 'BINTRAY_APIKEY'   : project.properties.bintrayApiKey
}

task download << {
    mkdir downloadDir
    vfs {
        if(!revealjsDir.exists()) {
            cp "zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
                revealjsDir, recursive: true, overwrite: true
        }
        if(!revealjsTemplateDir.exists()) {
            cp "zip:https://github.com/ysb33r/asciidoctor-reveal.js/archive/${revealjsTemplateVersion}.zip!asciidoctor-reveal.js-${revealjsTemplateVersion}/templates/slim",
                revealjsTemplateDir, recursive: true, overwrite: true
        }

    }

}

download {
    outputs.dir revealjsDir
    outputs.dir revealjsTemplateDir
}

asciidoctor {

//    requires 'asciidoctor-diagram'
//    inputs.dir 'src/gradleTest/codeSnippets/src'
    inputs.dir 'src/docs/asciidoc'

    sources {
        include 'cool-tools.adoc'
        include 'gr8-universe.adoc'
    }

    resources {
        from('src/docs/asciidoc') {
            include 'images/**'
            include 'stylesheets/**'
        }
        from("${outputDir}/generated-images") {
            include '**.png'
        }
        from (downloadDir) {
            include 'reveal.js/**'
        }
    }

    backends 'revealjs'
    options template_dirs : [ revealjsTemplateDir.parentFile.absolutePath ]

    attributes 'source-highlighter': 'coderay',
        'asciidoctorj' : asciidoctorj.version

    dependsOn download, test

    doLast {
        fileTree(outputDir) {
            include '**/*.html'
        }.files.each {
            it.text = it.text.replaceAll(/src="diag-/,'src="images/diag-')
        }
    }

}

